include sysshep/userprof.ublu
include sysshep/kill.ublu

\\ ${ Gets all messages waiting for a particular username, and prints them }$
FUNC sysshep.alarm_msgw ( propertiesfile qusername ignorepattern matchpattern extrakey ) $[
    \\ ${ Give a 10 minute timer before this script kills itself }$
    sysshep.killafter ( 600000 App|Ublu|Script|MSGW|Status )
    props -read @@propertiesfile

    LOCAL @username
    LOCAL @host
    LOCAL @password
    props -to @username -get sysshep.username
    props -to @host -get sysshep.host
    props -to @password -get sysshep.password

    LOCAL @as400

    LOCAL @logindpoint
    dpoint -to @logindpoint -dkey OS|AS400|Login -value 1 -type int -compare info -alertlevel 0
    @logindpoint -to @logindpoint -addkey @username

    TRY $[
        as400 -to @as400 @host @username @password

        LOCAL @test
        @as400 -to @test -validate
        IF @test THEN $[ \\ ${ Do nothing }$ ]$ ELSE $[ THROW ${ Login validation failure }$ ]$

        @logindpoint -msg ${ No problems logging in }$ -compare info
    ]$ CATCH $[
        LOCAL @body
        put -to @body ${ Could not log in as }$
        string -to @body -cat @body @username
        @logindpoint -msg @body -compare crit
        bye
    ]$

    sysshep.queue_from_username ( @as400 @@qusername )
    LOCAL @queue
    put -to @queue ~

    LOCAL @messages
    msgq -as400 @as400 -to @messages -reply @queue

    \\ ${ dpoint needs an alertlevel even for crit and warn comparators }$
    LOCAL @dpoint
    dpoint -to @dpoint -dkey OS|AS400|MESSAGES -value 1 -type int -compare crit -alertlevel 0
    @dpoint -to @dpoint -addkey @qusername

    test -to ~ -nnull @@extrakey
    IF ~ THEN $[
        @dpoint -to @dpoint -addkey @@extrakey
    ]$

    FOR @message in @messages $[
        LOCAL @body
        @message -to @body -message

        LOCAL @match
        tuple -true @match

        \\ ${ Test the match pattern if it is present }$
        test -to ~ -nnull @@matchpattern
        IF ~ THEN $[
            calljava -to @match -- @body -method matches -arg @@matchpattern
        ]$

        IF @match THEN $[
            LOCAL @ignore
            tuple -false @ignore

            \\ ${ Also check the ignore if it is present }$
            test -to ~ -nnull @@ignorepattern
            IF ~ THEN $[
                calljava -to @ignore -- @body -method matches -arg @@ignorepattern
            ]$
            IF -! @ignore THEN $[
                @dpoint -msg @body 
            ]$
        ]$
    ]$

    bye
]$
