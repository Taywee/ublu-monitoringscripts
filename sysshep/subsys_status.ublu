include sysshep/kill.ublu

\\ ${ Checks a subsystem status to make sure it matches a wanted status (usually }$
\\ ${ *ACTIVE) }$
FUNC sysshep.subsys_status ( propertiesfile subsyspath wantedstatus ) $[
    \\ ${ Give a 10 minute timer before this script kills itself }$
    sysshep.killafter ( 600000 App|Ublu|Script|SUBSYSSTATUS|Status )
    props -read @@propertiesfile

    LOCAL @username
    LOCAL @host
    LOCAL @password
    props -to @username -get sysshep.username
    props -to @host -get sysshep.host
    props -to @password -get sysshep.password

    LOCAL @as400

    LOCAL @logindpoint
    dpoint -to @logindpoint -dkey OS|AS400|Login -value 1 -type int -compare info -alertlevel 0
    @logindpoint -to @logindpoint -addkey @username

    TRY $[
        as400 -to @as400 @host @username @password

        LOCAL @test
        @as400 -to @test -validate
        IF @test THEN $[ \\ ${ Do nothing }$ ]$ ELSE $[ THROW ${ Login validation failure }$ ]$

        @logindpoint -msg ${ could log in just fine }$ -compare info
    ]$ CATCH $[
        LOCAL @body
        put -to @body ${ Could not log in as }$
        string -to @body -cat @body @username
        @logindpoint -msg @body -compare crit
        bye
    ]$

    subsys -as400 @as400 -to @subsys -new -subsyspath @@subsyspath
    @subsys -refresh

    LOCAL @status
    @subsys -to @status -query status

    LOCAL @test
    string -to @test -eq @status @@wantedstatus

    LOCAL @dpoint
    dpoint -to @dpoint -dkey OS|AS400|Subsystem -value 1 -type int -compare info -alertlevel 0
    @dpoint -to @dpoint -addkey @@subsyspath
    @dpoint -to @dpoint -addkey Status

    IF @test THEN $[
        @dpoint -msg ${ Subsystem status matched the expected value }$ -compare info
    ]$ ELSE $[
        LOCAL @body
        put -to @body ${ Subsystem }$
        string -to @body -cat @body @@subsyspath
        string -to @body -bl @body
        string -to @body -cat @body ${ status was }$
        string -to @body -cat @body @status
        string -to @body -bl @body
        string -to @body -cat @body ${ instead of the expected }$
        string -to @body -cat @body @@wantedstatus
        string -to @body -cat @body ${ ! }$
        @dpoint -msg @body -compare crit
    ]$
    bye
]$
