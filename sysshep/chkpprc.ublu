include sysshep/kill.ublu

\\ ${ Runs the CHKPPRC command with the specified env and type, then tails the log }$
\\ ${ out to the console }$
FUNC sysshep.chkpprc ( propertiesfile env type ) $[
    \\ ${ Give a 10 minute timer before this script kills itself }$
    sysshep.killafter ( 600000 App|Ublu|Script|CHKPPRC|Status )
    props -read @@propertiesfile

    LOCAL @username
    LOCAL @host
    LOCAL @password
    props -to @username -get sysshep.username
    props -to @host -get sysshep.host
    props -to @password -get sysshep.password

    \\ ${ build the command string }$
    LOCAL @command
    put -to @command ${ CHKPPRC ENV( }$
    string -to @command -trim @command
    string -to @command -cat @command @@env
    string -to @command -cat @command ${ ) TYPE( }$
    string -to @command -trim @command
    string -to @command -cat @command @@type
    string -to @command -cat @command ${ ) }$

    LOCAL @as400
    as400 -to @as400 @host @username @password

    commandcall -as400 @as400 @command

    \\ ${ build command to tail log, as we can not fetch log directly through ftp for some reason }$
    put -to @command ${ QSH CMD('tail -n1 /QIBM/qzrdhasm/qzrdhasm.log > /tmp/qzrdhasm.sysshep') }$

    commandcall -as400 @as400 @command

    \\ ${ must ftp file, because qsh output can't be received, only showing the exit status, not stdout }$
    LOCAL @result
    oldftp -get /tmp/qzrdhasm.sysshep @host @username @password
    put -to @result -from /tmp/qzrdhasm.sysshep

    LOCAL @teststring
    put -to @teststring ${ SUCCESSFUL, ready for the SWPPRC command. (chkpprc) }$
    string -to @teststring -trim @teststring

    \\ ${ check if the chkpprc was successful }$
    LOCAL @test
    calljava -to @test -- @result -method contains -arg @teststring

    LOCAL @dpoint
    dpoint -to @dpoint -dkey OS|AS400|CHKPPRC -alertlevel 0 -type int
    @dpoint -to @dpoint -addkey @@env
    @dpoint -to @dpoint -addkey @@type

    IF @test THEN $[
        @dpoint -msg ${ CHKPPRC check successful }$ -value 0 -compare info
    ]$ ELSE $[
        @dpoint -msg ${ CHKPPRC check FAILED! }$ -value 2 -compare crit
    ]$
    bye
]$
